policyresources
| where type == "microsoft.authorization/policyassignments"
| extend
    policyDefinitionId = tostring(properties.policyDefinitionId),
    assignmentDisplayName = properties.displayName,
    assignmentDescription = properties.description,
    scopeType = split(properties.scope, "/")[-2],
    scopeName = split(properties.scope, "/")[-1],
    scope = tostring(properties.scope)
| join kind=leftouter(policyresources | where type in ("microsoft.authorization/policydefinitions", "microsoft.authorization/policysetdefinitions")) on $left.policyDefinitionId == $right.id
| join kind=leftouter resourcecontainers on $left.scope == $right.id
| project
    name,
    policyDefinitionId,
    policyDefinitionDisplayName = properties1.displayName,
    policyDefinitionDescription = properties1.description,
    assignmentDisplayName,
    assignmentDescription,
    scopeType,
    scopeName = iff(isnotnull(properties2.displayName), properties2.displayName, name2),
    location,
    identity = tostring(identity),
    fullProperties = tostring(properties)