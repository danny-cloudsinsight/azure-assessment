authorizationresources
| where type == "microsoft.authorization/roleassignments"
| extend
    scopeType =  case (properties.scope =="/", "managementGroups",
                        properties.scope startswith("/providers/Microsoft.Management/"), "managementGroups",
                        properties.scope startswith("/providers/Microsoft.Subscription/aliases"), "aliases",
                        properties.scope contains("/providers/"), strcat_array(array_slice(split(split(properties.scope,"/providers")[-1],"/"),0,(array_length(split(split(properties.scope,"/providers")[-1],"/"))-2)), "/"),
                        properties.scope contains("/resourceGroups/"), "resourceGroups",
                        "subscriptions" ),
    id = tostring(properties.roleDefinitionId),
    scope = tostring(properties.scope)
| join kind=leftouter(
    authorizationresources
    | where type == "microsoft.authorization/roledefinitions"
) on id
| join kind=leftouter resourcecontainers on $left.scope == $right.id
| extend
    roleName = properties1.roleName,
    scopeName = case (properties.scope == "/", "root", 
                        scopeType in ("managementGroups", "subscriptions"), iff(isnotnull(properties2.displayName), properties2.displayName, name2),
                        split(properties.scope,"/")[-1])
| project
    scopeType,
    scopeName,
    roleName,
    principalType = properties.principalType,
    principalId = properties.principalId,
    createdOn = format_datetime(todatetime(properties.createdOn), "dd/MM/yyyy hh:mm:ss"),
    createdBy = properties.createdBy,
    updatedOn = format_datetime(todatetime(properties.updatedOn), "dd/MM/yyyy hh:mm:ss"),
    updatedBy = properties.updatedBy
